import eval;

func st_eval_prim_fn_body(clq *clqueue, env *st_env, paramorefs *array[st_obj_ref], retvalref st_obj_ref, prent *prim_fn_body) bool {
  switch &prent->op {
  case &PrimNum(a primitive_numeric_op):
    return st_eval_prim_num(clq, env, paramorefs, retvalref, &a);
  case &PrimNumCompare(a primitive_numeric_comparison_op):
    return st_eval_prim_num_compare(clq, env, paramorefs, retvalref, &a);
  case &PrimLogical(a primitive_logical_op):
    return st_eval_prim_logical(clq, env, paramorefs, retvalref, a);
  case &PrimPtrCompare(a primitive_ptr_comparison_op):
    return st_eval_prim_ptr_compare(clq, env, paramorefs, retvalref, &a);
  case &PrimConversion(a primitive_conversion_op):
    return st_eval_prim_conversion(clq, env, paramorefs, retvalref, &a);
  }
}

func st_eval_prim_conversion(clq *clqueue, env *st_env, paramorefs *array[st_obj_ref], retvalref st_obj_ref, op *primitive_conversion_op) bool {
  ps array[bigint];
  // Conversion operators have arity 1.
  if !st_nums(env, paramorefs, 1, op->from_size, op->from_numtraits.repr, &ps) {
    return false;
  }
  if retvalref.count != op->to_size {
    ice(_u8("st_eval_prim_conversion bad retvalref size"));
  }

  // We don't trap overflow if _either_ numeric type is non-trapping.
  nevertrap never_trap;
  if op->from_numtraits.trap_overflow {
    nevertrap = NeverTrapNo(void);
  } else {
    nevertrap = NeverTrapYes(void);
  }
  return st_num_rangechecked_output(clq, env, retvalref, get(&ps, 0), op->to_size, &op->to_numtraits, nevertrap);
}

func st_eval_prim_logical(clq *clqueue, env *st_env, paramorefs *array[st_obj_ref], retvalref st_obj_ref, op primitive_logical_op) bool {
  ps array[bigint];
  if !st_nums(env, paramorefs, op_arity(op), env->plat.bnno_size, Unsigned(void), &ps) {
    return false;
  }
  if retvalref.count != env->plat.booltraits.size {
    ice(_u8("st_eval_prim_logical bad retvalref size"));
  }

  result bool;
  switch op {
  case LogicalNot(v void):
    result = bigu(0) == get(&ps, 0);
  case BoolEq(v void):
    result = get(&ps, 0) == get(&ps, 1);
  case BoolNe(v void):
    result = get(&ps, 0) != get(&ps, 1);
  }
  resultint bigint;
  if result {
    resultint = bigu(1);
  } else {
    resultint = bigu(0);
  }
  val st_value = st_bigint_to_value_unsigned(&env->plat, retvalref.count, resultint);
  if !obj_write(env, retvalref, &val) {
    ice(_u8("st_eval_prim_logical cannot write into retvalref"));
  }
  return true;
}

func st_eval_prim_ptr_compare(clq *clqueue, env *st_env, paramorefs *array[st_obj_ref], retvalref st_obj_ref, op *primitive_ptr_comparison_op) bool {
  vals array[st_value];
  if !st_values(env, paramorefs, 2, env->plat.ptrtraits.size, &vals) {
    return false;
  }
  if retvalref.count != env->plat.booltraits.size {
    ice(_u8("st_eval_prim_ptr_compare bad retvalref size"));
  }
  a st_pointer;
  b st_pointer;
  if !st_value_as_pointer(&env->plat, ref(&vals, 0), &a) || !st_value_as_pointer(&env->plat, ref(&vals, 1), &b) {
    return false;
  }
  result bool;
  switch op->op_action {
  case CmpEq(v void):
    result = a.num == b.num && a.offset == b.offset;
  case CmpNe(v void):
    result = a.num != b.num || a.offset != b.offset;
  default:
    if a.num != b.num {
      ERR(_u8("Static eval cross-object pointer comparison."));
      return false;
    }
    switch op->op_action {
    case CmpLt(v void):
      result = a.offset < b.offset;
    case CmpGt(v void):
      result = a.offset > b.offset;
    case CmpLe(v void):
      result = a.offset <= b.offset;
    case CmpGe(v void):
      result = a.offset >= b.offset;
    }
  }
  // TODO: This could be deduped in this file.
  resultint bigint;
  if result {
    resultint = bigu(1);
  } else {
    resultint = bigu(0);
  }
  val st_value = st_bigint_to_value_unsigned(&env->plat, retvalref.count, resultint);
  if !obj_write(env, retvalref, &val) {
    ice(_u8("st_eval_prim_ptr_compare cannot write into retvalref"));
  }
  return true;
}

func st_eval_prim_num_compare(clq *clqueue, env *st_env, paramorefs *array[st_obj_ref], retvalref st_obj_ref, op *primitive_numeric_comparison_op) bool {
  ps array[bigint];
  // Comparison ops all take 2 arguments.
  if !st_nums(env, paramorefs, 2, op->op_size, op->op_numtraits.repr, &ps) {
    return false;
  }
  if retvalref.count != env->plat.booltraits.size {
    ice(_u8("st_eval_prim_num_compare bad retvalref size"));
  }

  a bigint = get(&ps, 0);
  b bigint = get(&ps, 1);
  result bool;
  switch op->op_action {
  case CmpEq(v void): result = a == b;
  case CmpNe(v void): result = a != b;
  case CmpLt(v void): result = a < b;
  case CmpGt(v void): result = a > b;
  case CmpLe(v void): result = a <= b;
  case CmpGe(v void): result = a >= b;
  }
  resultint bigint;
  if result {
    resultint = bigu(1);
  } else {
    resultint = bigu(0);
  }
  val st_value = st_bigint_to_value_unsigned(&env->plat, retvalref.count, resultint);
  if !obj_write(env, retvalref, &val) {
    ice(_u8("st_eval_prim_num_compare cannot write into retvalref"));
  }
  return true;
}

func st_eval_prim_num(clq *clqueue, env *st_env, paramorefs *array[st_obj_ref], retvalref st_obj_ref, op *primitive_numeric_op) bool {
  ps array[bigint];
  if !st_nums(env, paramorefs, op_arity(op->op_action), op->op_size, op->op_numtraits.repr, &ps) {
    return false;
  }
  if retvalref.count != op->op_size {
    ice(_u8("st_eval_prim_arith bad retvalref size"));
  }

  switch op->op_action {
  case NumAdd(v void):
    return st_num_rangechecked_output(clq, env, retvalref, get(&ps, 0) + get(&ps, 1), op);
  case NumSub(v void):
    return st_num_rangechecked_output(clq, env, retvalref, get(&ps, 0) - get(&ps, 1), op);
  case NumMul(v void):
    return st_num_rangechecked_output(clq, env, retvalref, get(&ps, 0) * get(&ps, 1), op);
  case NumDiv(v void):
    // TODO: This does not have x86-compatible (or platform-compatible) behavior for signed division.
    div_result bigint;
    mod_discard bigint;
    if !st_divmod(get(&ps, 0), get(&ps, 1), op, &div_result, &mod_discard) {
      return false;
    }
    return st_num_rangechecked_output(clq, env, retvalref, div_result, op);
  case NumMod(v void):
    // TODO: This does not have x86-compatible (or platform-compatible) behavior for signed division.
    div_discard bigint;
    mod_result bigint;
    if !st_divmod(get(&ps, 0), get(&ps, 1), op, &div_discard, &mod_result) {
      return false;
    }
    return st_num_rangechecked_output(clq, env, retvalref, mod_result, op);
  case NumNegate(v void):
    return st_num_rangechecked_output(clq, env, retvalref, - get(&ps, 0), op);
  case NumBitAnd(v void):
    return st_num_rangechecked_output(clq, env, retvalref, get(&ps, 0) & get(&ps, 1), op);
  case NumBitOr(v void):
    return st_num_rangechecked_output(clq, env, retvalref, get(&ps, 0) | get(&ps, 1), op);
  case NumBitXor(v void):
    return st_num_rangechecked_output(clq, env, retvalref, get(&ps, 0) ^ get(&ps, 1), op);
  case NumBitNot(v void):
    return st_num_rangechecked_output(clq, env, retvalref, ^get(&ps, 0), op, NeverTrapYes(void));
  case NumShiftLeft(v void):
    // TODO: Exactly how should compile-time bitshifting work?  What if we overflow a signed value?
    bitsize u32 = op->op_size * env->plat.bnno_size;
    rhs bigint = get(&ps, 1);
    rhs32 u32;
    if !as_non_negative_u32(&rhs, &rhs32) || rhs32 >= bitsize {
      ERR(_u8("Leftshift rhs out of range"));
      return false;
    }
    return st_num_rangechecked_output(clq, env, retvalref, get(&ps, 0) << rhs32, op, NeverTrapYes(void));
  case NumShiftRight(v void):
    bitsize u32 = op->op_size * env->plat.bnno_size;
    rhs bigint = get(&ps, 1);
    rhs32 u32;
    if !as_non_negative_u32(&rhs, &rhs32) || rhs32 >= bitsize {
      ERR(_u8("Rightshift rhs out of range"));
      return false;
    }
    return st_num_rangechecked_output(clq, env, retvalref, get(&ps, 0) >> rhs32, op, NeverTrapYes(void));
  }
}

func st_divmod(x bigint, y bigint, op *primitive_numeric_op, div_out *bigint, mod_out *bigint) bool {
  if y == bigu(0) {
    ERR(_u8("Static eval: divide by zero"));
    return false;
  }
  switch op->op_numtraits.repr {
  case Unsigned(v void): { }
  case SignedTwos(v void):
    if y == -bigu(1) && x == op->op_numtraits.minval {
      ERR(_u8("Static eval: divide minval by -1"));
      return false;
    }
  }
  dm tup[bigint, bigint] = divmod(x, y);
  // If numerator is negative, twiddle result to obey "official" SignedTwos (x86) semantics.  divmod already handles the x>0, y>0 case (duh) and the x>0, y<0 case correctly.
  if dm.cdr != bigu(0) && is_negative(&x) {
    if is_negative(&y) {
      // div is positive, we need it smaller.
      dm.car = dm.car - bigu(1);
      // lessens the remainder
      dm.cdr = dm.cdr + y;
    } else {
      // div is negative, we need it bigger.
      dm.car = incr(dm.car);
      // lessens the remainder
      dm.cdr = dm.cdr - y;
    }
  }
  *div_out = dm.car;
  *mod_out = dm.cdr;
  return true;
}

// Says that we should _not_ trap on overflow -- used for bitwise ops where it's possible and OK to overflow (namely, bitwise not).
defenum never_trap {
  NeverTrapNo void;
  NeverTrapYes void;
};

func st_num_rangechecked_output(clq *clqueue, env *st_env, retvalref st_obj_ref, num bigint, op *primitive_numeric_op) bool {
  return st_num_rangechecked_output(clq, env, retvalref, num, op, NeverTrapNo(void));
}

func st_num_rangechecked_output(clq *clqueue, env *st_env, retvalref st_obj_ref, num bigint, op *primitive_numeric_op, nevertrap never_trap) bool {
  return st_num_rangechecked_output(clq, env, retvalref, num, op->op_size, &op->op_numtraits, nevertrap);
}

func st_num_rangechecked_output(clq *clqueue, env *st_env, retvalref st_obj_ref, num bigint, op_size u32, op_numtraits *numeric_traits, nevertrap never_trap) bool {
  if num < op_numtraits->minval || num > op_numtraits->maxval {
    if case NeverTrapNo(v void) = nevertrap {
      if op_numtraits->trap_overflow {
        ERR(_u8("Result out of range."));
        return false;
      }
    }
    // Make sure we know to handle some weird reprs later when we have them.
    switch op_numtraits->repr {
    case Unsigned(void):
      // do nothing
    case SignedTwos(void):
      // do nothing
    }
    addand bigint = incr(op_numtraits->maxval - op_numtraits->minval);
    num = ((num - op_numtraits->minval) % addand) + op_numtraits->minval;
  }

  val st_value;
  if !st_bigint_to_value_twos(&env->plat, op_size, num, &val) {
    ice(_u8("st_num_rangechecked_input just rangechecked bigint, could not convert to st_value."));
  }
  obj_write(env, retvalref, &val);
  return true;
}

func st_values(env *st_env, paramorefs *array[st_obj_ref], op_arity u32, op_size u32, out *array[st_value]) bool {
  nparams size = count(paramorefs);
  if op_arity != ~nparams {
    ice(_u8("st_nums numeric op bad arity"));
  }
  ret array[st_value];
  for i size = 0; i < nparams; i = i + 1 {
    oref st_obj_ref = get(paramorefs, i);
    if oref.count != op_size {
      ice(_u8("st_nums bad operand size"));
    }
    val st_value;
    if !obj_read(env, oref, &val) {
      return false;
    }
    push(&ret, val);
  }
  *out = ret;
  return true;
}

func st_nums(env *st_env, paramorefs *array[st_obj_ref], op_arity u32, op_size u32, repr numeric_representation, out *array[bigint]) bool {
  vals array[st_value];
  if !st_values(env, paramorefs, op_arity, op_size, &vals) {
    return false;
  }
  ret array[bigint];
  nparams size = count(&vals);
  for i size = 0; i < nparams; i = i + 1 {
    oref st_obj_ref = get(paramorefs, i);
    if oref.count != op_size {
      ice(_u8("st_nums bad operand size"));
    }
    val st_value;
    if !obj_read(env, oref, &val) {
      return false;
    }

    num bigint;
    switch repr {
    case Unsigned(void):
      if !st_value_unsigned_as_bigint(&env->plat, ref(&vals, i), &num) {
        return false;
      }
    case SignedTwos(void):
      if !st_value_signed_twos_as_bigint(&env->plat, ref(&vals, i), &num) {
        return false;
      }
    }
    push(&ret, num);
  }
  *out = ret;
  return true;
}
