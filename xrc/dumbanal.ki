import state;

enum genexpr_result {
  Terminal void;
  NonTerminal void;
}

func dumbly_analyze_xnode(h *frame, gr *frame_graph, ctr *celltrack, sn sq_num,
                          snc *sn_annot, xn *gr_xnode) void {
  switch &xn->op {
  case &GrApply(a gr_apply):
    // Retcell needs to be live _before_ the funcall happens.
    expect_live(ctr, a.retcell);

    note_lower_paramlists(h, sn, ctr);
    annotate_ref(&snc->app_time_live, ctr);

    nparams size = count(&a.params);

    switch a.act {
    case TwoExtraOps(ops gr_active_xop):
      gr_track_active_op(ctr, ops.op1);
      gr_track_active_op(ctr, ops.op2);
    case StandardApply:
      void;
    }
    gr_deactivate(ctr, a.funcell);
    for i size = 0; i < nparams; i = i + 1 {
      gr_deactivate(ctr, get(&a.params, i));
    }
    gr_activate(ctr, a.retcell);

    deaden(ctr, a.funcell);
    for i size = 0; i < nparams; i = i + 1 {
      deaden(ctr, get(&a.params, i));
    }

  case &GrPrimApply(a gr_prim_apply):
    expect_live(ctr, a.retcell);

    nparams size = count(&a.params);
    for i size = 0; i < nparams; i = i + 1 {
      gr_deactivate(ctr, get(&a.params, i));
    }
    gr_activate(ctr, a.retcell);

    for i size = 0; i < nparams; i = i + 1 {
      deaden(ctr, get(&a.params, i));
    }

  case &GrMemCopy(a gr_memcopy):
    expect_live(ctr, a.dest);
    expect_live(ctr, a.src);

  case &GrWriteConst(a gr_writeconst):
    expect_live(ctr, a.dest);

  case &GrAddressof(a gr_addressof):
    expect_live(ctr, a.addressee);
    expect_live(ctr, a.dest);
    gr_activate(ctr, a.dest);

  case &GrDeref(a gr_deref):
    expect_live(ctr, a.pointer);
    check(!isLocationStatic(ref_cell(gr, a.name)->location));
    create(h, ctr, a.name);

  case &GrSubcell(a gr_subcell):
    expect_live(ctr, a.partof);
    check(!isLocationStatic(ref_cell(gr, a.name)->location));
    create(h, ctr, a.name);

  case &GrLive(a gr_live):
    create(h, ctr, a.cell);

  case &GrAssertLive(a gr_assert_live):
    expect_live(ctr, a.cell);

  case &GrDead(a gr_dead):
    deaden(ctr, a.cell);

  case &GrVirtualDead(a gr_virtual_dead):
    deaden(ctr, a.cell);

  case &GrActiveXop(a gr_active_xop):
    gr_track_active_op(ctr, a.op1);
    gr_track_active_op(ctr, a.op2);

  case &GrManyActiveXop(a gr_many_active_xop):
    nops size = count(&a.ops);
    for i size = 0; i < nops; i = i + 1 {
      gr_track_active_op(ctr, get(&a.ops, i));
    }
  }
}

func dumbly_analyze_expression(h *frame, gr *frame_graph, ctr *celltrack, sn sq_num) cr[genexpr_result] {
  for ;; {
    gnc *sn_annot = ref_annot(h, sn);
    if case &Computed(checksum celltrack_checksum) = &gnc->plainly_live {
      // TODO: We should also check that we're part of the same sequential _family_ of exprs.
      if !matches_checksum(ctr, &checksum) {
        return ERR(_u8("ICE: Inconsistent liveness/active state when revisiting node ("), sn.x.x,
                   _u8("), celltrack="), *ctr);
      }
      return NoFail(NonTerminal);
    }

    annotate(&gnc->plainly_live, get_checksum(ctr));

    qn *gr_qnode = ref_qnode(gr, sn);
    if true {
      switch &qn->op {
      case &GrBranch(a gr_branch):
        expect_live(ctr, a.src);

        end_ctr celltrack;
        combined_mut genexpr_result = NonTerminal;
        ntargets size = branch_target_count(&a);
        for i size = 0; i < ntargets; i++ {
          target *gr_branch_target = branch_target_ref(&a, i);
          new_live celltrack = *ctr;
          casmut genexpr_result = #dumbly_analyze_expression(h, gr, &new_live, target->sn);
          combine_branch_mut(&new_live, &combined_mut, casmut, &end_ctr);
        }

        // Yes this is empty of it's NonTerminal -- it doesn't matter.
        *ctr = end_ctr;
        return NoFail(combined_mut);

      case &GrSequence(a gr_sequence):
        xn *gr_xnode = ref_xnode(gr, a.first);
        dumbly_analyze_xnode(h, gr, ctr, sn, gnc, xn);
        sn = a.second;
        // Loop around.

      case &GrJmp(a gr_jmp):
        sn = a.next;
        // Loop around.

      case &GrQNop:
        return NoFail(Terminal);
      }
    }
  }
}

func note_lower_paramlists(h *frame, our_sn sq_num, live_at_app_time *celltrack) void {
  paramlists array[sq_num];
  nall size = count(&live_at_app_time->all);
  for i size = 0; i < nall; i = i + 1 {
    c cell_num = ref(&live_at_app_time->all, i)->cell;
    if case Has(sn sq_num) = ref(&h->bas->celldisp, c.x)->paramlist {
      if sn != our_sn {
        push(&paramlists, sn);
        discard bool = try_insert(&ref_annot(h, sn)->higher_paramlists, &our_sn, void);
      }
    }
  }

  sort(&paramlists);
  dedup_sorted(&paramlists);
  annotate_move(&ref_annot(h, our_sn)->lower_paramlists, &paramlists);
}

func combine_branch_mut(new_live *celltrack, onto *genexpr_result, case_mut genexpr_result, end_ctr *celltrack) void {
  switch case_mut {
  case Terminal:
    switch *onto {
    case Terminal:
      if !matches_checksum(new_live, end_ctr) {
        ice(_u8("terminal/terminal new_live not equal, end_celltrack="), *end_ctr, _u8(", new_live="), *new_live);
      }
    case NonTerminal:
      *onto = Terminal;
      *end_ctr = *new_live;
    }
  case NonTerminal:
    void;
  }
}

// For deaden and other functions, prelive shows what was alive _before_ prec_muts modified it.
func deaden(ctr *celltrack, c cell_num) void {
  ctr_deaden(ctr, c);
}

func expect_live(ctr *celltrack, c cell_num) void {
  ctr_expect_live(ctr, c);
}

func create(h *frame, ctr *celltrack, c cell_num) void {
  ctr_create(ctr, c);
  note_conflicting(h, &ctr->all, c);
}

func note_conflicting(h *frame, all *array[celltrack_entry], c cell_num) void {
  // This also means conflicting is set to non-empty for any cell that's made live.
  c_disp *cell_disp = ref(&h->bas->celldisp, c.x);
  nall size = count(all);
  for i size = 0; i < nall; i = i + 1 {
    d cell_num = ref(all, i)->cell;
    d_disp *cell_disp = ref(&h->bas->celldisp, d.x);
    push(&c_disp->conflicting, d);
    push(&d_disp->conflicting, c);
  }
}

func ref_annot(h *frame, sn sq_num) *sn_annot {
  return ref(&h->by_sn, sn.x.x);
}
