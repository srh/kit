import ast;
import shray;

func[H, T] build_array_raw_u8str(help H, r *array[u8], buf *T, n size) void {
  push(r, '[');
  for i size = 0; i < n; i = i + 1 {
    if i != 0 {
      push(r, ',');
    }
    build_u8str(help, r, &buf[i]);
  }
  push(r, ']');
}

func[H, T] build_array_u8str(help H, r *array[u8], arr *array[T]) void {
  build_array_raw_u8str(help, r, data(arr), count(arr));
}

func[H, T] build_shray_u8str(help H, r *array[u8], arr *shray[T]) void {
  build_array_raw_u8str(help, r, data(arr), count(arr));
}

func[H, T] to_array_u8str(help H, arr array[T]) array[u8] {
  ret array[u8];
  build_array_u8str(help, &ret, &arr);
  return ret;
}

func[H, T] to_shray_u8str(help H, arr shray[T]) array[u8] {
  ret array[u8];
  build_shray_u8str(help, &ret, &arr);
  return ret;
}

func[H, T] wrap_u8str(help H, r *array[u8], x array[u8], y *T) void {
  append(r, &x);
  push(r, '(');
  build_u8str(help, r, y);
  push(r, ')');
}

func[H] build_u8str(help H, r *array[u8], a *ast_typeexpr) void {
  switch a {
    case &Name(b ast_ident): {
      wrap_u8str(help, r, _u8("Name"), &b);
    }
    case &App(b ast_typeapp): {
      wrap_u8str(help, r, _u8("App"), &b);
    }
    case &Struct(b ast_struct): {
      wrap_u8str(help, r, _u8("Struct"), &b);
    }
    case &Enum(b ast_enum): {
      wrap_u8str(help, r, _u8("Enum"), &b);
    }
    case &Union(b ast_union): {
      wrap_u8str(help, r, _u8("Union"), &b);
    }
    case &Arraytype(b ast_arraytype): {
      wrap_u8str(help, r, _u8("Arraytype"), &b);
    }
    case &Unknown(b ast_unknown): {
      wrap_u8str(help, r, _u8("Unknown"), &b);
    }
  }
}

func[H] build_u8str(help H, r *array[u8], a *ast_ident) void {
  append(r, _u8("{value:"));
  build_u8str(help, r, &a->value);
  append(r, _u8("}"));
}

func[H] build_u8str(help H, r *array[u8], a *ast_typeapp) void {
  append(r, _u8("{typefunc:"));
  build_u8str(help, r, &a->typefunc);
  append(r, _u8(",params:"));
  build_array_u8str(help, r, &a->params);
  push(r, '}');
}

func[H] build_u8str(help H, r *array[u8], a *ast_struct) void {
  append(r, _u8("{fields:"));
  build_array_u8str(help, r, &a->fields);
  push(r, '}');
}

func[H] build_u8str(help H, r *array[u8], a *ast_enum) void {
  append(r, _u8("{constructors:"));
  build_array_u8str(help, r, &a->constructors);
  push(r, '}');
}

func[H] build_u8str(help H, r *array[u8], a *ast_union) void {
  append(r, _u8("{fields:"));
  build_array_u8str(help, r, &a->fields);
  push(r, '}');
}

func[H] build_u8str(help H, r *array[u8], a *ast_arraytype) void {
  append(r, _u8("{count:"));
  build_u8str(help, r, &a->count);
  append(r, _u8(",param:"));
  build_u8str(help, r, &a->param);
  push(r, '}');
}

func[H] build_u8str(help H, r *array[u8], a *ast_unknown) void {
  append(r, _u8("ast_unknown"));
  // Do nothing.
}

func[H] build_u8str(help H, r *array[u8], a *ast_vardecl) void {
  append(r, _u8("{name:"));
  build_u8str(help, r, &a->name);
  append(r, _u8(",type:"));
  build_u8str(help, r, &a->type);
  push(r, '}');
}

func[H, T] build_u8str(help H, r *array[u8], a *opt[T]) void {
  switch a {
    case &None: {
      append(r, _u8("None"));
    }
    case &Has(b T): {
      append(r, _u8("Has("));
      build_u8str(help, r, &b);
      push(r, ')');
    }
  }
}

func[H, T] build_u8str(help H, r *array[u8], a *box[T]) void {
  build_u8str(help, r, oo(a));
}

func[H, T] build_u8str(help H, r *array[u8], a *shp[T]) void {
  build_u8str(help, r, oo(a));
}

func[H] build_u8str(help H, r *array[u8], a *ast_expr) void {
  append(r, _u8("ast_expr"));
}

